plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    modCompileOnly("dev.architectury:architectury-fabric:${rootProject.architectury_version}") { exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" }

    // for sodium compat
    modRuntimeOnly "maven.modrinth:sodium:mc1.21-0.5.9"

    // for iris compat
    modRuntimeOnly "maven.modrinth:iris:1.7.1+1.21"
    modRuntimeOnly 'org.anarres:jcpp:1.4.14' //temp iris
    modRuntimeOnly 'io.github.douira:glsl-transformer:2.0.0-pre13' //temp iris

    // for REI compat
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-fabric:14.0.688") { exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" exclude group: "dev.architectury" }

    // for Pehkui compat
    modCompileOnly("com.github.Virtuoel:Pehkui:3.7.12") { exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" }

    // other mods
    // modRuntimeOnly("com.simibubi.create:create-fabric-1.19.2:0.5.0.i-969+1.19.2")
    // modCompileOnly "maven.modrinth:lithium:mc1.20.4-0.12.1"
    // modCompileOnly "maven.modrinth:indium:1.0.30+mc1.20.4"

    //mod menu
    modApi("com.terraformersmc:modmenu:11.0.1") { exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" }

    // for immersive portals compat
    modCompileOnly('com.github.iPortalTeam:ImmersivePortalsMod:v5.1.7-mc1.20.4') { exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" }

    // needed for resource loading
    include(modImplementation(fabricApi.module("fabric-resource-loader-v0", rootProject.fabric_api_version)))
    // commands also needs base
    include(modImplementation(fabricApi.module("fabric-api-base", rootProject.fabric_api_version)))
    include(modImplementation(fabricApi.module("fabric-command-api-v2", rootProject.fabric_api_version)))
    // network registry// network registry
    include(modImplementation(fabricApi.module("fabric-networking-api-v1", rootProject.fabric_api_version)))

    include(implementation('com.electronwill.night-config:toml:3.6.6'))
    include(implementation('com.electronwill.night-config:core:3.6.6'))

    include(implementation("org.lwjgl:lwjgl-openvr:3.3.3"))
    include(implementation("org.lwjgl:lwjgl-openvr:3.3.3:natives-linux"))
    include(implementation("org.lwjgl:lwjgl-openvr:3.3.3:natives-macos"))
    include(implementation("org.lwjgl:lwjgl-openvr:3.3.3:natives-windows"))

}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set('fabric')
}

jar {
    archiveClassifier.set("dev")
    manifest {
        attributes(
            'Main-Class': "org.vivecraft.LaunchPopup"
        )
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
